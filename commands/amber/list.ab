import { array_shift, array_contains } from "std/array"

import { info, fatal } from "../../lib/utils.ab"
import { get_amber_tags } from "../../lib/github.ab"
import { get_local_versions } from "../../lib/amber_manager.ab"

fun show_help_and_exit() {
    echo "Usage: amethyst amber list [options]"
    echo ""
    echo "Options:"
    echo "  --all,  -a    List all available amber versions"
    echo "  --help, -h    Show this help message and exit"
    exit(0)
}

pub fun cmd_amber_list(arguments: [Text]): Null {
    let show_remote = false

    loop {
        if len(arguments) == 0 {
            break
        }

        const arg = trust array_shift(arguments)
        if {
            arg == "--all" or arg == "-a": show_remote = true
            arg == "--help" or arg == "-h": show_help_and_exit()
            else: fatal("Unknown argument: " + arg)
        }
    }

    const local_versions = get_local_versions()
    if not show_remote {
        info("Listing local amber versions:")
        for version in local_versions {
            echo "- {version}"
        }

        return null
    }

    info("Listing all amber versions:")
    const tags = get_amber_tags()
    for tag in tags {
        const is_local = array_contains(local_versions, tag)

        echo "- {tag} {is_local then "[installed]" else ""}"
    }

    exit(0)
}
