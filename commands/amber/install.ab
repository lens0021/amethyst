import { array_shift, array_contains } from "std/array"
import { join } from "std/text"

import { info, warn, fatal } from "../../lib/utils.ab"
import { get_amber_tags } from "../../lib/github.ab"
import { get_local_versions, is_available_locally, download } from "../../lib/amber_manager.ab"

fun show_help_and_exit(code: Num = 0): Null {
    echo "Usage: amethyst amber install <version> [options]"
    echo ""
    echo "Arguments:"
    echo "  <version>     The version of amber to install"
    echo "                Use 'latest' to install the latest version"
    echo ""
    echo "Options:"
    echo "  --force, -f    Force re-install version."
    echo "  --help,  -h    Show this help message and exit"
    exit(code)
}

pub fun cmd_amber_install(arguments: [Text]): Null {
    let version = ""
    let force = false

    loop {
        if len(arguments) == 0 {
            break
        }

        const arg = trust array_shift(arguments)
        if {
            arg == "--force" or arg == "-f": force = true
            arg == "--help" or arg == "-h": show_help_and_exit()
            else: version = arg
        }
    }

    if version == "" {
        fatal("No version specified.")
    }

    const tags = get_amber_tags()
    
    if version == "latest"
        and len(tags) == 0 {
        fatal("No amber versions found.")
    }

    if version == "latest" {
        version = tags[0]
    }
    
    if not array_contains(tags, version) {
        let available_versions = ""
        for tag in tags {
            available_versions += "- {tag}\n"
        }
        fatal("Couldn't find version {version}, available versions are:\n{available_versions}")
    }

    const is_installed = is_available_locally(version)

    if is_installed {
        warn("Amber version {version} is already installed.")

        if not force {
            warn("Use --force to re-install.")
            exit(1)
        }
    }

    info("Installing amber version {version}...")
    download(version)

    info("Amber version {version} installed successfully.")

    exit(0)
}
